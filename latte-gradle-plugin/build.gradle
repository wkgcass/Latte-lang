def VERSION_FILE = file('../latte-build/src/main/resources/version')
BufferedReader br = new BufferedReader(new FileReader(VERSION_FILE))
def VERSION = br.readLine().trim()
br.close()

group 'org.latte-lang'
version VERSION

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: LatteBuild

compileGroovy {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.encoding = "UTF-8"
}

repositories {
    mavenLocal()
}

dependencies {
    compile group: 'org.latte-lang', name: 'latte-compiler', version: VERSION
    compile gradleApi()
    compile localGroovy()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def deploy_user = System.getenv('DEPLOY_USER')
            def deploy_pass = System.getenv('DEPLOY_PASS')

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deploy_user, password: deploy_pass)
            }

            pom.project {
                name 'latte-gradle-plugin'
                packaging 'jar'
                description 'The latte-lang gradle plugin, which helps compile latte sources in a gradle project.'
                url 'http://latte-lang.org'
                scm {
                    url 'https://github.com/wkgcass/Latte-lang'
                    connection 'scm:git:https://github.com/wkgcass/Latte-lang.git'
                    developerConnection 'scm:git:https://github.com/wkgcass/Latte-lang.git'
                }
                licenses {
                    license {
                        name 'MIT LICENSE'
                        url 'https://github.com/wkgcass/Latte-lang/blob/master/LICENSE/'
                    }
                }
                developers {
                    developer {
                        id 'wkgcass'
                        email 'wkgcass@hotmail.com'
                    }
                }
            }
        }
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
    }
}

signing {
    sign configurations.archives
}

class LatteBuild implements Plugin<Project> {
    @Override
    void apply(Project project) {
        def latteBuild = project.task('latteBuild')
        latteBuild.dependsOn project.tasks['install']

        project.task('latteTest') // do nothing

        def latteDeploy = project.task('latteDeploy')
        latteDeploy.dependsOn project.tasks['uploadArchives']
    }
}
